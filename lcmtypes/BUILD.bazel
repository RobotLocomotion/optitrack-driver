licenses(["notice"])

package(default_visibility = ["//visibility:public"])

load("@lcm//lcm-bazel:lcm_cc_library_srcs.bzl", "lcm_cc_library_srcs")
load("@lcm//lcm-bazel:lcm_java_library_srcs.bzl", "lcm_java_library_srcs")
load("@lcm//lcm-bazel:lcm_library.bzl", "lcm_library")
load("@lcm//lcm-bazel:lcm_py_library_srcs.bzl", "lcm_py_library_srcs")
load("@rules_cc//cc:cc_library.bzl", "cc_library")
load("@rules_java//java:java_library.bzl", "java_library")
load("@rules_python//python:py_library.bzl", "py_library")

LCM_SRCS = [
    "optitrack_data_descriptions_t.lcm",
    "optitrack_frame_t.lcm",
    "optitrack_marker_set_description_t.lcm",
    "optitrack_marker_set_t.lcm",
    "optitrack_marker_t.lcm",
    "optitrack_rigid_body_t.lcm",
    "optitrack_rigid_body_description_t.lcm",
    "optitrack_rigid_body_marker_description_t.lcm",
    "optitrack_skeleton_description_t.lcm",
    "optitrack_skeleton_t.lcm",
    "optitrack_forceplate_description_t.lcm",
    "optitrack_forceplate_t.lcm",
]

# Copy the srcs in a package-specific subdir.
[
    genrule(
        name = "_cp_" + x,
        srcs = [x],
        outs = ["optitrack/" + x],
        cmd = "cp $< $@",
        visibility = ["//visibility:private"],
    )
    for x in LCM_SRCS
]

filegroup(
    name = "lcm_srcs_with_subdir",
    srcs = [
        ":optitrack/" + x
        for x in LCM_SRCS
    ],
)

lcm_library(
    name = "optitrack_lcm_library",
    srcs = [":lcm_srcs_with_subdir"],
    lcm_package = "optitrack",
)

lcm_cc_library_srcs(
    name = "optitrack_cc_srcs",
    src = ":optitrack_lcm_library",
)

cc_library(
    name = "optitrack_lcmtypes",
    srcs = [":optitrack_cc_srcs"],
    deps = ["@lcm//lcm:lcm-coretypes"],
    includes = ["."],
)

filegroup(
    name = "cxx_srcs",
    srcs = [":optitrack_cc_srcs"],
)

lcm_py_library_srcs(
    name = "optitrack_py_srcs",
    src = ":optitrack_lcm_library",
)

py_library(
    name = "py_optitrack_lcmtypes",
    srcs = [":optitrack_py_srcs"],
    imports = ["."],
)

lcm_java_library_srcs(
    name = "optitrack_java_srcs",
    src = ":optitrack_lcm_library",
)

java_library(
    name = "lcmtypes_optitrack",
    srcs = [":optitrack_java_srcs"],
    deps = ["@lcm//lcm-java"],
)

filegroup(
    name = "java_srcs",
    srcs = [":optitrack_java_srcs"],
)
